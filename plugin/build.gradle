plugins {
    id 'kotlin'
    id "java-gradle-plugin"
    alias(libs.plugins.kotlin.jvm)
}

sourceSets {
    main.java.srcDirs += "$buildDir/generated/build/com/aleyn/plugin"
}

group = libs.versions.lrouterGroup.get()
version = libs.versions.lrouterVersion.get()
project.ext.artifactId = "plugin"

def map = new HashMap<String, String>()
map.put('version', libs.versions.lrouterVersion.get())
map.put('routerDepend', "${libs.versions.lrouterGroup.get()}:core:")
map.put('processorDepend', "${libs.versions.lrouterGroup.get()}:processor:")

tasks.register('genVersion', Copy) {
    from 'src/main/build'
    into "$buildDir/generated/build/com/aleyn/plugin"
    expand(map)
    filteringCharset = 'UTF-8'
}

compileKotlin.dependsOn('genVersion')

tasks.register('copyJar', Copy) {
    from "${buildDir}/libs/plugin-${version}.jar"
    into "../plugins"
    filteringCharset = 'UTF-8'
    rename { String name ->
        name.replace("-${version}", '')
    }
}

jar.finalizedBy('copyJar')

dependencies {
    implementation gradleApi()
    compileOnly libs.gradle
    compileOnly libs.symbol.processing.gradle.plugin
    implementation libs.asmCore
    implementation libs.asm.commons
    implementation libs.gson
}

gradlePlugin {
    website = 'https://github.com/aleyn97/router'
    vcsUrl = 'https://github.com/aleyn97/router.git'
    plugins {
        create("routerPlugin") {
            id = "aleyn-router"
            implementationClass = "com.aleyn.router.plug.RouterPlugin"
            displayName = "LRouter Plugin"
            description = "LRouter code injection plugin"
            tags.set(["android", "router"])
        }
    }
}

apply from: '../publishing.gradle'